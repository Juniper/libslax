<?xml version="1.0" encoding="UTF-8" standalone="yes"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:exsl="http://exslt.org/common" version="1.0" extension-element-prefixes="exsl">
  <!-- This stylesheet will:
- reorder, indent and normalize a comps file,
- merge duplicate groups and categories,
- warn about packages referenced by multiple groups,
- kill multiple references to the same package within a group,

Typical usage is:
$ xsltproc -o output-file comps-cleanup.xsl original-file

You can use the ‑‑novalid xsltproc switch to kill the warning about
Fedora not installing the comps DTD anywhere xsltproc can find it.
However without DTD there is no way to check the files completely.

© Nicolas Mailhot <nim at fedoraproject dot org> 2006 -->
  <xsl:strip-space elements="*"/>
  <xsl:output method="xml" indent="yes" encoding="UTF-8" doctype-system="comps.dtd" doctype-public="-//Red Hat, Inc.//DTD Comps info//EN"/>
  <xsl:key name="unique-groups" match="/comps/group" use="id/text()"/>
  <xsl:key name="unique-categories" match="/comps/category" use="id/text()"/>
  <xsl:key name="packages-by-group" match="/comps/group/packagelist/packagereq" use="../../id/text()"/>
  <xsl:key name="unique-package-entries" match="/comps/group/packagelist/packagereq" use="concat(../../id/text(), &quot;/&quot;, text())"/>
  <xsl:key name="unique-packages" match="/comps/group/packagelist/packagereq[generate-id(.) = generate-id(key(&quot;unique-package-entries&quot;, concat(../../id/text(), &quot;/&quot;, text()))[1])]" use="text()"/>
  <xsl:key name="groups-by-category" match="/comps/category/grouplist/groupid" use="../../id/text()"/>
  <xsl:key name="unique-group-entries" match="/comps/category/grouplist/groupid" use="concat(../../id/text(), &quot;/&quot;, text())"/>
  <xsl:variable name="lcletters" select="&quot;abcdefghijklmnopqrstuvwxyz&quot;"/>
  <xsl:variable name="ucletters" select="&quot;ABCDEFGHIJKLMNOPQRSTUVWXYZ&quot;"/>
  <xsl:variable name="type-sort-order">
    <unknown/>
    <mandatory/>
    <conditional/>
    <default/>
    <optional/>
  </xsl:variable>
  <xsl:variable name="attribute-sort-order">
    <unknown/>
    <type/>
    <requires/>
    <basearch/>
  </xsl:variable>
  <!-- Preserve most nodes -->
  <xsl:template match="*" priority="0">
    <xsl:apply-templates select="." mode="normalize"/>
  </xsl:template>
  <xsl:template match="*" mode="normalize">
    <!-- Group comments with the logically-following element -->
    <xsl:apply-templates select="preceding-sibling::node()[normalize-space()][1][self::comment()]"/>
    <xsl:copy>
      <xsl:apply-templates select="@*">
        <xsl:sort select="count(exsl:node-set($attribute-sort-order)/*[name() = name(current())]/preceding-sibling::*)" data-type="number"/>
      </xsl:apply-templates>
      <xsl:apply-templates select="* | text()"/>
    </xsl:copy>
  </xsl:template>
  <!-- Preserve attributes and text nodes -->
  <xsl:template match="comment() | text()">
    <xsl:apply-templates select="preceding-sibling::node()[normalize-space()][1][self::comment()]"/>
    <xsl:copy/>
  </xsl:template>
  <!-- Preserve attributes -->
  <xsl:template match="@*">
    <xsl:copy/>
  </xsl:template>
  <!-- Sort groups by id, and categories by display order -->
  <xsl:template match="comps" priority="1">
    <xsl:apply-templates select="preceding-sibling::node()[normalize-space()][1][self::comment()]"/>
    <xsl:copy>
      <xsl:apply-templates select="group">
        <xsl:sort select="translate(id/text(), $lcletters, $ucletters)"/>
      </xsl:apply-templates>
      <xsl:apply-templates select="category">
        <xsl:sort select="display_order/text()"/>
        <xsl:sort select="translate(id/text(), $lcletters, $ucletters)"/>
      </xsl:apply-templates>
    </xsl:copy>
  </xsl:template>
  <!-- Warn about empty groups -->
  <xsl:template match="group[count(key(&quot;packages-by-group&quot;, id/text())) = 0]" priority="2">
    <xsl:message>
      <xsl:text>☹☹☹ Empty group </xsl:text>
      <xsl:value-of select="concat(_name/text(), &quot; (&quot;, id/text(), &quot;)&quot;)"/>
      <xsl:text>!</xsl:text>
    </xsl:message>
    <xsl:apply-templates select="." mode="normalize"/>
  </xsl:template>
  <!-- Warn about duplicate groups being merged -->
  <xsl:template match="group[generate-id(.) != generate-id(key(&quot;unique-groups&quot;, id/text())[1])]" priority="3">
    <xsl:message>
      <xsl:text> ☹☹ Duplicate group </xsl:text>
      <xsl:value-of select="concat(_name/text(), &quot; (&quot;, id/text(), &quot;)&quot;)"/>
      <xsl:text> will be merged.</xsl:text>
    </xsl:message>
  </xsl:template>
  <!-- Warn about empty categories -->
  <xsl:template match="category[count(key(&quot;groups-by-category&quot;, id/text())) = 0]" priority="2">
    <xsl:message>
      <xsl:text>☹☹☹ Empty category </xsl:text>
      <xsl:value-of select="concat(_name/text(), &quot; (&quot;, id/text(), &quot;)&quot;)"/>
      <xsl:text>!</xsl:text>
    </xsl:message>
    <xsl:apply-templates select="." mode="normalize"/>
  </xsl:template>
  <!-- Warn about duplicate categories being merged -->
  <xsl:template match="category[generate-id(.) != generate-id(key(&quot;unique-categories&quot;, id/text())[1])]" priority="3">
    <xsl:message>
      <xsl:text> ☹☹ Duplicate category </xsl:text>
      <xsl:value-of select="concat(_name/text(), &quot; (&quot;, id/text(), &quot;)&quot;)"/>
      <xsl:text> will be merged.</xsl:text>
    </xsl:message>
  </xsl:template>
  <!-- Sort packages within a group by class then name -->
  <xsl:template match="packagelist" priority="1">
    <xsl:copy>
      <xsl:apply-templates select="key(&quot;packages-by-group&quot;, ../id/text())">
        <xsl:sort select="count(exsl:node-set($type-sort-order)/*[name() = current()/@type]/preceding-sibling::*)" data-type="number"/>
        <xsl:sort select="translate(text(), $lcletters, $ucletters)"/>
      </xsl:apply-templates>
    </xsl:copy>
  </xsl:template>
  <!-- Sort groups within a category by name -->
  <xsl:template match="category/grouplist" priority="1">
    <xsl:copy>
      <xsl:apply-templates select="key(&quot;groups-by-category&quot;, ../id/text())">
        <xsl:sort select="translate(text(), $lcletters, $ucletters)"/>
      </xsl:apply-templates>
    </xsl:copy>
  </xsl:template>
  <!-- Kill duplicate package entries -->
  <xsl:template match="packagereq[generate-id(.) != generate-id(key(&quot;unique-package-entries&quot;, concat(../../id/text(), &quot;/&quot;, text()))[1])]" priority="2">
    <xsl:message>
      <xsl:text>☹☹☹ Ignoring duplicate reference to </xsl:text>
      <xsl:value-of select="concat(@type, &quot; package &quot;, text())"/>
      <xsl:text> in group </xsl:text>
      <xsl:value-of select="concat(../../_name/text(), &quot; (&quot;, ../../id/text(), &quot;)&quot;)"/>
      <xsl:text>.</xsl:text>
    </xsl:message>
    <xsl:message>
      <xsl:text>     ⇒ Only its first reference (</xsl:text>
      <xsl:value-of select="key(&quot;unique-package-entries&quot;, concat(../../id/text(), &quot;/&quot;, text()))[1]/@type"/>
      <xsl:text> package) will be kept.</xsl:text>
    </xsl:message>
  </xsl:template>
  <!-- Kill duplicate group entries -->
  <xsl:template match="category/grouplist/groupid[generate-id(.) != generate-id(key(&quot;unique-group-entries&quot;, concat(../../id/text(), &quot;/&quot;, text()))[1])]" priority="1">
    <xsl:message>
      <xsl:text>  ☹ Ignoring duplicate reference to group </xsl:text>
      <xsl:value-of select="text()"/>
      <xsl:text> in category </xsl:text>
      <xsl:value-of select="concat(../../_name/text(), &quot; (&quot;, ../../id/text(), &quot;)&quot;)"/>
      <xsl:text>.</xsl:text>
    </xsl:message>
  </xsl:template>
  <!-- Warn about packages referenced several times
<xsl:template match="packagereq[generate-id(.) = generate-id(key('unique-packages',text())[2])]" priority="1">
<xsl:variable name="dupes" select="key('unique-packages',text())"/>
<xsl:message>  ☹ Package <xsl:value-of select="text()"/> is referenced in <xsl:value-of select="count($dupes)"/> groups:</xsl:message>
<xsl:for-each select="$dupes">
<xsl:sort select="translate(../../id/text(),$lcletters,$ucletters)"/>
<xsl:message>     ✓ <xsl:value-of select="@type"/> package in group <xsl:value-of select="concat(../../_name/text(),' (',../../id/text(),')')"/></xsl:message>
</xsl:for-each>
<xsl:apply-templates select="." mode="normalize"/>
</xsl:template> -->
</xsl:stylesheet>
